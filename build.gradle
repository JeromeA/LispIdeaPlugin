plugins {
    id 'org.jetbrains.intellij' version '1.3.0'
    id 'java'
    id 'com.intershop.gradle.javacc' version '4.1.3'
}

group 'org.ax1'
version '0.1.3'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation "com.google.truth.extensions:truth-java8-extension:1.1.3"
}

sourceSets.main.java.srcDirs 'src/main/gen'

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.1'
}

patchPluginXml {
    changeNotes = """
      This is a very early release.<br>
      Don't use it."""
    sinceBuild = "211"
    untilBuild = "242"
}

test {
    useJUnitPlatform()
}

javacc {
    javaCCVersion = '7.0.12'
    configs {
        loopparser {
            inputFile = file('src/main/javacc/LoopParser.jj')
            packageName = 'org.ax1.lisp.loop'
            outputDir = file('src/main/gen')
        }
    }
}

signPlugin {
  certificateChain = providers.environmentVariable("LISP_PLUGIN_CERTIFICATE_CHAIN")
  privateKey = providers.environmentVariable("LISP_PLUGIN_PRIVATE_KEY")
  password = providers.environmentVariable("LISP_PLUGIN_PRIVATE_KEY_PASSWORD")
}

publishPlugin {
  token = providers.environmentVariable("LISP_PLUGIN_PUBLISH_TOKEN")
}

