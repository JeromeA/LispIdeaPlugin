{
  generate = [java="8"]
  parserClass="org.ax1.lisp.parsing.LispParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lisp"
  psiImplClassSuffix="Impl"
  psiPackage="org.ax1.lisp.psi"
  psiImplPackage="org.ax1.lisp.psi.impl"

  elementTypeHolderClass="org.ax1.lisp.psi.LispTypes"
  elementTypeClass="org.ax1.lisp.psi.LispElementType"
  tokenTypeClass="org.ax1.lisp.psi.LispTokenType"

  implements("sexp")="org.ax1.lisp.psi.impl.LispSexpMixin"
  implements("list")="org.ax1.lisp.psi.impl.LispListMixin"
  implements("symbol")="org.ax1.lisp.psi.impl.LispSymbolMixin"
  mixin("sexp")="org.ax1.lisp.psi.impl.LispSexpMixinImpl"
  mixin("list")="org.ax1.lisp.psi.impl.LispListMixinImpl"
  mixin("symbol")="org.ax1.lisp.psi.impl.LispSymbolMixinImpl"

}

lispFile ::= topLevelUnit_*
private topLevelUnit_ ::= syntaxUnit_ | invalidClose
private syntaxUnit_ ::= COMMENT | SHARP_SIGN_PLUS_KEYWORD_PACAKGE | sexp
sexp ::= STRING | NUMBER | CHARACTER | symbol | list | quoted
list ::= (LPAREN syntaxUnit_* RPAREN?)
symbol ::= SYMBOL_TOKEN
invalidClose ::= RPAREN
quoted ::= QUOTE sexp
