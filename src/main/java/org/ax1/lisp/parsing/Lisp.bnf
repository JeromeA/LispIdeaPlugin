{
  generate = [java="8"]
  parserClass="org.ax1.lisp.parsing.LispParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lisp"
  psiImplClassSuffix="Impl"
  psiPackage="org.ax1.lisp.psi"
  psiImplPackage="org.ax1.lisp.psi.impl"

  elementTypeHolderClass="org.ax1.lisp.psi.LispTypes"
  elementTypeClass="org.ax1.lisp.psi.LispElementType"
  tokenTypeClass="org.ax1.lisp.psi.LispTokenType"

  implements("sexp")="org.ax1.lisp.psi.impl.LispSexpMixin"
  mixin("sexp")="org.ax1.lisp.psi.impl.LispSexpMixinImpl"

  implements("list")="org.ax1.lisp.psi.impl.LispListMixin"
  mixin("list")="org.ax1.lisp.psi.impl.LispListMixinImpl"

  implements("symbol_name|package_prefix|string_content")="org.ax1.lisp.psi.impl.LispStringDesignator"
//  mixin("symbol_name|package_prefix|string_content")="org.ax1.lisp.psi.impl.LispStringDesignatorImpl"
  extends("symbol_name|package_prefix|string_content")="org.ax1.lisp.stubs.LispStringDesignatorStubBase<?>"
  elementTypeFactory("symbol_name|package_prefix|string_content")="org.ax1.lisp.stubs.LispElementTypeFactory.stubFactory"
}

lispFile ::= topLevelUnit_*

private topLevelUnit_ ::= syntaxUnit_ | invalidClose
private syntaxUnit_ ::= COMMENT | SHARP_UNSUPPORTED | prefixed_sexp
prefixed_sexp ::= reader_feature* reader_prefix? sexp
reader_prefix ::= SHARP_PREFIX

reader_feature ::= SHARP_PLUS feature_exp
feature_exp ::= simple_feature_exp | compound_feature_exp
simple_feature_exp ::= colon? symbol_name
compound_feature_exp ::= LPAREN feature_exp* RPAREN?

sexp ::= string | NUMBER | CHARACTER | SHARP_REF | symbol | list | vector | quoted
string ::= STRING_QUOTE string_content STRING_QUOTE
string_content ::= STRING_CONTENT_TOKEN?
  {stubClass="org.ax1.lisp.stubs.LispStringContentStub"}
list ::= LPAREN syntaxUnit_* RPAREN?
symbol ::= (package_prefix? colon)? symbol_name
colon ::= COLON_TOKEN
package_prefix ::= PACKAGE_TOKEN
  {stubClass="org.ax1.lisp.stubs.LispPackagePrefixStub"}
symbol_name ::= SYMBOL_TOKEN
  {stubClass="org.ax1.lisp.stubs.LispSymbolNameStub"}
invalidClose ::= RPAREN
vector ::= SHARP_LPAREN syntaxUnit_* RPAREN?
quoted ::= QUOTE sexp
